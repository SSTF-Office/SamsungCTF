apply plugin: 'com.android.application'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.sctf2019.manife"
        minSdkVersion 28
        targetSdkVersion 28

        // ToDo: BuildConfig.VERSION_CODE
        versionCode 137

        // ToDo: BuildConfig.VERSION_NAME
        versionName "1.3.7-00-13371-global"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("release.keystore")
            storePassword "5L6rdsMEKHK6FwMa"
            keyAlias "releaseKey"
            keyPassword "5L6rdsMEKHK6FwMa"
            v1SigningEnabled true
            v2SigningEnabled false
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

// Remove original AndroidManifest.xml from assembled APKs
// Use manife.apk as a challenge file
afterEvaluate {
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            variant.getAssembleProvider().configure() {
                it.doLast {
                    def file = output.outputFile
                    println "[+] outputFile = $file"
                    assert file.exists()

                    def out = "${file.parentFile.path}/manife.apk"

                    exec {
                        executable "cp"
                        args file.path, out
                    }

                    exec {
                        executable "zip"
                        args "-d", out, "AndroidManifest.xml"
                    }

                    println "[+] AndroidManifest removed: $out"
                }
            }
        }
    }
}
